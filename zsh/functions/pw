function pw(){
    # pass directory
    local prefix="${PASSWORD_STORE_DIR:-$HOME/.password-store}"

    # pass timeout
    local timeout="${PASSWORD_STORE_CLIP_TIME:-45}"

    # last xclip content
    local xclip_content_before="$(xclip -o)"

    # xclip command
    local XCLIP=(xclip -selection primary -loops 1 -quiet)

    # password var
    local pw="$@"

    # check if exact match given
    if [[ ! -e "${prefix}/${pw}.gpg" ]]
    then
        # search for pass using completion helper
        local pass_results="$(find -L "${prefix}" -type f -not -path '*.git*' -not -name '.gpg-id' | sed -e "s#${prefix}/##" -e 's#\.gpg$##' | sort | grep -i "${pw}")"
        local no_pass_results="$(echo "${pass_results}" | grep -c .)"

        # parse results
        if [[ "$no_pass_results" -gt 1 ]]
        then
            # print all results
            echo "${pass_results}"
            return
        elif [[ "$no_pass_results" -eq 1 ]]
        then
            # print only result
            local pw="${pass_results}"

            echo "${0}: Only password matching search: '${pw}'"
            echo
        elif [[ "$no_pass_results" -eq 0 ]]
        then
            # no pw match
            echo "${0}: No password match for: '${pw}'"

            return 1
        fi
    fi

    # get pass content
    local pass_info="$(pass show "${pw}")"

    # show non confidential stuff
    echo "${pass_info}" | tail -n +2

    # copy username if it exists
    echo "${pass_info}" | grep -qiE 'user(name)?:'
    if [[ "$?" -eq 0 ]]
    then
        # notify user
        echo
        echo "Copying username of '${pw}' to clipboard..."

        # copy username
        echo -n "${pass_info}" | grep -iE 'user(name)?:' | cut -d: -f2- | sed 's/^\s*//' | tr -d '\n' | $XCLIP 2> /dev/null
    fi

    # notify user
    echo "Now copying password of '${pw}' to clipboard (only one chance to paste)!"

    # copy password
    echo -n "$pass_info" | head -n1 | tr -d '\n' | $XCLIP 2> /dev/null

    # restore clipboard
    echo -n "${xclip_content_before}" | xclip -selection primary
    echo
    echo 'Previous clipboard restored.'
}

# vim: ft=zsh
